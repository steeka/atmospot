apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.json:json:20151123'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.22.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.httpcomponents:httpclient:4.5.2'
}

test {
    testLogging {
        showStandardStreams true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

checkstyle {
    ignoreFailures true
}

pmd {
    ruleSetFiles = files('config/pmd/pmd-rulesets.xml')
    consoleOutput true
    ignoreFailures true
}

task docker(type:Exec) {
    dependsOn war
    workingDir rootProject.buildDir.getPath() + '/docker'
    commandLine 'docker-compose', '-p', rootProject.name, 'up', '--build', '-d'
    doFirst {
        copy {
            from 'tools/docker'
            into workingDir
        }
        copy {
            from tasks.war.archivePath
            into workingDir.getPath() + '/tomcat'
        }
    }
}

test {
    dependsOn docker
}

task dockerRemove {
    doFirst {
        exec {
            workingDir docker.workingDir
            commandLine 'docker-compose', '-p', rootProject.name, 'stop'
        }
        exec {
            workingDir docker.workingDir
            commandLine 'docker-compose', '-p', rootProject.name, 'rm', '-f'
        }
    }
}
